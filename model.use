model Delivery


enum Etat_Box {Free, Occupied, Full}
enum Etat_Command {Waiting,Assigned,Finished}
enum Etat_Drone {Created, WaitingForCommand, Assigned, Charging, OnWarehouse, OnAPoint, OnABox, FullBattery, Loaded, Empty}
--_________________________
--CLASSES
--_________________________

class Point
	attributes 
		x : Integer
		y : Integer	
	operations
		-- Computing the distance between two points 
		d(p2 : Point ) : Integer =
			(self.x - p2.x).abs + (self.y - p2.y).abs
		-- Déterminer si à un point, il y a une Box ou une Warehouse
end

class Box < Point
    attributes
        capacity : Integer
        state : Etat_Box init = Etat_Box::Free
    operations 
        voisins() : Set(Box) =
            Box.allInstances() -> select (n | d(n) < 3)
        voisinsPlus() : Set(Box) =
            self.voisins() -> closure (b|b.voisins())
        distance(d : Drone) : Integer =
            (self.x-d.position4drone.x).abs + (self.y-d.position4drone.y).abs
    	setOccupied()
    	begin
    		self.state := Etat_Box::Occupied;
		end
		setFree()
		begin
			self.state := Etat_Box::Free;
		end
		setFull()
		begin
			self.state := Etat_Box::Full;
		end
	statemachines
    psm StateMachineForBox
        states
        	startUp:initial
            free
            full
            occupied
         transitions
         	startUp -> free { create }
            free -> occupied {[Drone.allInstances()->one(d | distance(d) = 0)] setOccupied()}
            occupied -> free {[Drone.allInstances()->forAll(d | distance(d) <> 0)] setFree()}
            free -> full {[self.capacity = 50] setFull()}	--RCAP
            occupied -> full {[self.capacity = 50] setFull()}
            full -> free {[self.capacity = 0 and Drone.allInstances()->forAll(d | distance(d) <> 0)] setFree()}
            full -> occupied {[self.capacity = 0 and Drone.allInstances()->one(d | distance(d) = 0)] setOccupied()}
    end
end

class Drone

	attributes 
		energy : Integer init = 3
		capacity : Integer init = 0
		state : Etat_Drone init = Etat_Drone::WaitingForCommand
	operations

		distance(p:Point) : Integer = (self.position4drone.x - p.x).abs + (self.position4drone.y - p.y).abs;
		
		assignate(c : Command)
		begin
			insert(c, self) into delivery;
			self.state := Etat_Drone::Assigned;
		end

		fill()
		begin
			for p in self.cmd4drone.product4cmd do
				self.capacity := self.capacity + p.weight
			end;
			self.state := Etat_Drone::Loaded;
		end

		recharge ()
		begin
			-- TODO : time ++
			self.energy := self.energy +1;
			self.state := Etat_Drone::Charging;
		end

		unloadAndValidate()
		begin
			declare b1 : Set(Box);
			--Remplir la box la où on est. 
			b1 := Box.allInstances().select( b | distance(b) = 0);
			for b in b1 do
				b.capacity := b.capacity + self.capacity;
			end;

			self.capacity := 0;
			delete (self.cmd4drone, self) from delivery;
		end

		move (moveX : Integer, moveY : Integer)
		begin
			declare p : Point;
			p := Point.allInstances().select(p | p.x = self.position4drone.x + moveX and p.y = self.position4drone.y + moveY)->asSequence()->first();
			delete (self,self.position4drone) from positionD;
			insert (self, p) into positionD;
			--self.position4drone.x := self.position4drone.x + moveX;
			--self.position4drone.y := self.position4drone.y + moveY;
			self.energy := self.energy -1;
			
		end

		setOnWarehouse()
		begin
			self.state := Etat_Drone::OnWarehouse
		end

		setOnABox()
		begin
			self.state := Etat_Drone::OnABox
		end

		setOnAPoint()
		begin
			self.state := Etat_Drone::OnAPoint
		end

		setEmpty()
		begin
			self.state := Etat_Drone::Empty;
		end

	statemachines
		psm StateMachineOfADrone
			states
				startUp:initial
				waitingForCommand
				commandAssigned
				productsCharged
				onAPoint
				onABox
				onWarehouse
				atDeliveryBox
				unloaded
				batteryCharging
			transitions
				startUp -> waitingForCommand { create }
				waitingForCommand -> commandAssigned { assignate() }
				commandAssigned -> productsCharged { fill() }
				productsCharged -> onWarehouse {[Warehouse.allInstances()->one(w | w.distance(self) = 0)] setOnWarehouse()}
				productsCharged -> onABox {[Box.allInstances()->one(b | b.distance(self) = 0)] setOnABox()}
				productsCharged -> onAPoint {[Point.allInstances()->one(p | distance(p) = 0)] setOnAPoint()}
				onWarehouse -> onABox {[Box.allInstances()->one(b | b.distance(self) = 0)] setOnABox()}
				onWarehouse -> onAPoint {[Point.allInstances()->one(p | distance(p) = 0)] setOnAPoint()}
				onWarehouse -> batteryCharging{[Box.allInstances()->one(b | b.distance(self) = 0) and self.energy < 3] recharge()}
				onAPoint -> onABox {[Box.allInstances()->one(b | b.distance(self) = 0)] setOnABox()}
				onAPoint -> onWarehouse {[Warehouse.allInstances()->one(w | w.distance(self) = 0)] setOnWarehouse()}
				onABox -> batteryCharging {[Box.allInstances()->one(b | b.distance(self) = 0) and self.energy < 3] recharge()}
				batteryCharging -> onABox {[Box.allInstances()->one(b | b.distance(self) = 0) and self.energy = 3] setOnABox()}
				batteryCharging -> onWarehouse {[Warehouse.allInstances()->one(w | w.distance(self) = 0) and self.energy = 3] setOnWarehouse()}
				onABox -> atDeliveryBox {[self.cmd4drone.cmdDestination.x = self.position4drone.x and self.cmd4drone.cmdDestination.y = self.position4drone.y] unloadAndValidate()}
				atDeliveryBox -> unloaded {[self.capacity = 0] setEmpty()}
				unloaded -> onAPoint {[Point.allInstances()->one(p | distance(p) = 0)] setOnAPoint()}
				unloaded -> onABox {[Box.allInstances()->one(b | b.distance(self) = 0)] setOnABox()}
		end
end

class Command
	attributes
		state : Etat_Command
	operations
	 	setAssigned()
	 	begin
	 		self.state := Etat_Command::Assigned
	 	end
	 	setFinished()
	 	begin
	 		self.state := Etat_Command::Finished
	 	end
	statemachines
		psm StateMachineForACommand
			states
				startUp:initial
				Waiting
				Assigned
				Finished:final
			transitions
				startUp -> Waiting { create }
				Waiting -> Assigned { [Drone.allInstances()->one(d|d.cmd4drone = self)] setAssigned() }
				Assigned -> Finished { [Drone.allInstances()->one(d|d.cmd4drone = self and d.position4drone.x = self.cmdDestination.x and d.position4drone.y = self.cmdDestination.y and d.capacity = 0)] setFinished() }
		end
end
class Warehouse < Box end
class Product
	attributes
		weight : Integer
end 

class Time end


--_______________________________
--CONTRAINTES
--_______________________________

constraints

--_____________
--Sur les classes
--_____________

context Warehouse inv oneWarehouse : Warehouse.allInstances->size=1


context b1,b2:Box inv noTwoBoxesAtSamePlace : 
	 (b1.x = b2.x) and 
	 (b1.y = b2.y)
	 implies b1 = b2


context d1,d2:Drone inv noTwoDronesAtSamePlace :
	(d1.position4drone.x = d2.position4drone.x) and
	(d1.position4drone.y = d2.position4drone.y)
	implies d1 = d2

context p:Point inv noTwoDronesAtSameSpot:
	if not p.oclIsTypeOf(Warehouse) then
		Drone.allInstances().select(d|d.distance(p) = 0)->size() <= 1 and
		Drone.allInstances().select(d|d.distance(p) = 0)->size() >= 0
	else
		Drone.allInstances().select(d|d.distance(p) = 0)->size() >= 0
	endif

context w:Warehouse inv neighbourBoxNearWarehouse :
    Box.allInstances() -> exists (b | d(b) < 3 )

context Drone inv batteryPositive : self.energy >= 0
context Drone inv batteryNotTooHigh : self.energy <= 3 
context Drone inv maxDroneNb : Drone.allInstances -> size <= 15
-- DNB
context Box inv maxBoxNb : Box.allInstances -> size <=20
-- RNB
context Drone inv droneCapacityPositive : self.capacity >= 0
context Drone inv droneCapacityMax : self.capacity <= 20 
-- DCAP
context Box inv boxCapacityPositive : self.capacity >= 0
context Box inv boxCapacityMax : self.capacity <= 50
-- RCAP


context b1:Box inv transitiveClosureComplete :
	b1.voisinsPlus()->includesAll(Box.allInstances())
--_____________
--Sur les fonctions (post et pré condition)
--_____________
	
context Drone::recharge()
	pre droneIsNotFullyRecharged: self.energy < 3
	pre droneIsAtBox: Box.allInstances->exists (b | b.distance(self) =0)
	post droneIsStillAtBox: Box.allInstances->exists (b | b.distance(self) =0)
	-- post rechargePost2 : tempsAprès = tempsAvant + nbRecharges

context Drone::fill()
	pre droneIsInTheWh: Warehouse.allInstances->exists(w | w.distance(self) =0)
	pre droneIsEmpty: self.capacity = 0
	--On considère qu'une commande correspond à un seul aller-retour
	post droneIsntTooCharged : self.capacity <=20

context Drone::assignate(c:Command)	
	--On considère qu'une commande correspond à un seul aller-retour
	pre droneIsInTheWh: Warehouse.allInstances->exists(w | w.distance(self) =0)
	pre noCommandYet: not Command.allInstances-> exists(c | self.cmd4drone = c) 

context Drone::unloadAndValidate()
	pre droneNotEmpty : self.capacity <> 0
	pre droneIsAtBox : Box.allInstances-> one(b | b.distance(self) =0) and not Warehouse.allInstances-> exists (w | w.distance(self) =0)
	pre boxIsDestination : self.cmd4drone.cmdDestination.x=self.position4drone.x and self.cmd4drone.cmdDestination.y = self.position4drone.y
	post droneIsEmpty : self.capacity = 0
	post droneIsStillAtBox : Box.allInstances-> one(b | b.distance(self) =0) and not Warehouse.allInstances-> exists (w | w.distance(self) =0)
	post boxCapacityIsntOverloaded : not Box.allInstances -> exists (b| b.capacity > 50)

context Drone::move(moveX:Integer, moveY:Integer)
	pre nothingAtDestination : not Drone.allInstances -> exists(d2 | d2.position4drone.x = self.position4drone.x + moveX and d2.position4drone.y = self.position4drone.y + moveY)
	pre droneEnergyIsGreaterThan1 : self.energy > 0
	pre exactlyOneUnitLenghtMove : moveX + moveY = 1
	post droneChangedLocation : (self.position4drone <> self.position4drone@pre)


--___________________________
--ASSOCIATIONS
--___________________________ 

association command between
	Command [1] role cmd4product
	Product [*] role product4cmd
end

association delivery between
	Command [1] role cmd4drone
	Drone [1] role drone4cmd
end

association destination between
	Command [0..1] role cmd4Box
	Box [1] role cmdDestination
end

association positionD between
	Drone [0..1] role drone4position
	Point [1] role position4drone
end
